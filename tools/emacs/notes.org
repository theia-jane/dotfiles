#+TITLE: Notes
#+PROPERTY: header-args :tangle-relative 'dir :dir ${HOME}/.local/emacs/site-lisp
#+PROPERTY: header-args+ :tangle config-notes.el

#+begin_src elisp
(provide 'config-notes)
#+END_SRC

* Config
#+begin_src elisp
(defvar notes-dir "~/Desktop/Notes/")
(setq org-directory notes-dir)
#+end_src

* Periodic notes
#+begin_src elisp
(defvar periodic-notes-subdir "periodic")

(defun periodic-increment-by (type n &optional time)
  (let* ((time (or time (current-time)))
         (decoded (decode-time time))
         (decoded_month (nth 4 decoded))
         (decoded_year (nth 5 decoded)))
    (pcase type
      ('day (time-add time (seconds-to-time (* n 24 60 60))))
      ('month (progn
               (calendar-increment-month decoded_month decoded_year n)
               (setf (nth 4 decoded) decoded_month
                     (nth 5 decoded) decoded_year)
               (encode-time decoded)))
      ('year (progn
              (setf (nth 5 decoded) (+ n decoded_year))
              (encode-time decoded))))))

(defun periodic-notes-dir ()
    (f-join
     notes-dir
     periodic-notes-subdir))

(defun periodic-notes-file-path (type &optional time)
  (let* ((format-path
         (pcase type
           ('year "%Y/year.org")
           ('month "%Y/%m/month.org")
           ('day "%Y/%m/%d.org")))
         (time
          (pcase time
            ('next (periodic-increment-by type 1))
            ('prev (periodic-increment-by type -1))
            (_ time))))
    (f-join (periodic-notes-dir)
            (format-time-string format-path time))))

(defun periodic-goto (type &optional time)
  (let ((filepath (periodic-notes-file-path type time)))
    (unless (file-exists-p filepath)
      (let ((dir (file-name-directory filepath)))
        (unless (file-exists-p dir)
          (make-directory dir t))))
        (find-file filepath)))

(defun periodic-goto-daily (&optional time)
  (interactive)
  (periodic-goto 'day time))

(defun periodic-goto-monthly (&optional time)
  (interactive)
  (periodic-goto 'month time))

(defun periodic-goto-yearly (&optional time)
  (interactive)
  (periodic-goto 'year time))

(defun periodic-note-p (&optional file)
   (f-descendant-of-p (or file default-directory) (periodic-notes-dir)))

(defun periodic-note-type (&optional file)
  (when (periodic-note-p file)
    (pcase (f-relative buffer-file-name (periodic-notes-dir))
      ((rx  (= 4 digit) "/year.org") 'year)
      ((rx  (= 4 digit) "/" (= 2 digit) "/month.org") 'month)
      ((rx  (= 4 digit) "/" (= 2 digit) "/" (= 2 digit) ".org") 'day)
      (_ nil))))

(defun periodic-note-type-p (type &optional file)
  (eq (periodic-note-type file) type))

(defun periodic-note-yearly-p (&optional file)
  (periodic-note-type-p 'year file))

(defun periodic-note-monthly-p (&optional file)
  (periodic-note-type-p 'month file))

(defun periodic-note-daily-p (&optional file)
  (periodic-note-type-p 'day file))

(set-file-template! 'org-mode :when 'periodic-note-daily-p :trigger "__daily-note.org")
(set-file-template! 'org-mode :when 'periodic-note-monthly-p :trigger "__monthly-note.org")
(set-file-template! 'org-mode :when 'periodic-note-yearly-p :trigger "__yearly-note.org")
#+end_src


* Interactive command
#+begin_src elisp
(defmacro notes-cmd (&rest body)
  `#'(lambda () (interactive)
       (let ((default-directory notes-dir))
         ,@body)))
#+end_src

* Keymapping
#+begin_src elisp
(map! :leader
      :prefix "n"
      :desc "New org buffer" "b" (notes-cmd
                                  (with-current-buffer (generate-new-buffer "*notes*")
                                    (org-mode)
                                    (switch-to-buffer (current-buffer))))
      :desc "Notes search" "/" (notes-cmd (+default/search-cwd))
      :desc "Notes dir" "d" (notes-cmd (command-execute #'dired))

      (:prefix-map ("p" . "periodic")
       :desc "Daily note"    "d" #'periodic-goto-daily
       :desc "Monthly note"  "m" #'periodic-goto-monthly
       :desc "Yearly note"   "y" #'periodic-goto-yearly
       (:prefix-map ("p" . "previous")
        :desc "Yesterday's note"   "d" (cmd! (periodic-goto-daily 'prev))
        :desc "Last month's note"  "m" (cmd! (periodic-goto-monthly 'prev))
        :desc "Last year's note"   "y" (cmd! (periodic-goto-yearly 'prev)))
       (:prefix-map ("n" . "next")
        :desc "Tomorrow's note"    "d" (cmd! (periodic-goto-daily 'next))
        :desc "Next month's note"  "m" (cmd! (periodic-goto-monthly 'next))
        :desc "Next year's note"   "y" (cmd! (periodic-goto-yearly 'next)))
       ))
#+end_src
* Invalidate cache
My notes cache is often out of date, I'm uncertain why it doesn't auto update -- it seems like it should, but when I go to find a file new notes don't just show up.

** Clear cache on start up
#+NAME: projectile-notes-clear-cache
#+begin_src elisp
(after! projectile
  (let ((default-directory notes-dir))
    (projectile-invalidate-cache nil)))
#+end_src

** Script file
Script that can be called from anywhere to clear notes cache
#+begin_src bash :tangle projectile-notes-clear-cache :dir ${HOME}/bin :shebang #!/bin/bash
emacsclient -e "$(cat <<ELISP
<<projectile-notes-clear-cache>>
ELISP
)"
#+end_src
** Watch for changes
:properties:
:header-args:conf: yes :dir ${HOME}/.config/systemd/user
:end:
Maybe there is an emacs/projectile specific way to do this, but I don't know it and want to play around with systemd.
To use you must run =systemctl --user enable notes.path=
*** =notes.path=
:PROPERTIES:
:header-args:conf+: :tangle notes.path
:END:
First we create a path unit to watch my notes.

We have to wait for the emacs daemon to be up to start doing any work.
#+BEGIN_SRC conf
[Unit]
Description=Watch notes directory for changes
Wants=emacs.service
#+END_SRC

=PathModified= is slightly more noisy compared to =PathChanged=, but it picks up on more as well. Note that this does /not/ watch the entire tree, just the subdirectory for changes. Luckily for me the sync logs get updated anytime there are changes to the files, so this works.
#+BEGIN_SRC conf
[Path]
PathModified=%h/Desktop/Notes/
Unit=notes.service

[Install]
WantedBy=default.target
#+END_SRC

*** =notes.service=
This adds a service for =notes.path= to trigger, we set the limit settings to make sure only one gets fired off each second.
#+BEGIN_SRC conf :tangle notes.service
[Unit]
Description=Invalidate notes projectile cache
StartLimitIntervalSec=1
StartLimitBurst=1

[Service]
ExecStart=%h/bin/projectile-notes-clear-cache
#+END_SRC
